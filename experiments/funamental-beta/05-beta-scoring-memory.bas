5 REM (L.19.3) Program Listing for FUNDAMENTAL BETA WITH SCORING AND MEMORY DISPLAY
10 REM FUNDAMENTAL BETA WITH
15 REM SCORING AND MEMORY DISPLAY
20 REM TRS-80
25 REM
30 REM
100 REM ** BETA PSYCH MAINLINE, V.2-TRS-80 **
105 CLEAR 128:F$="#.##"
110 CLS:PRINT STRING$(6,26):PRINT TAB(18);
115 PRINT "FUNDAMENTAL BETA WITH SCORING"
120 PRINT:PRINT "STRIKE 'ENTER' TO START ..."
125 S$=INKEY$:IF S$="" THEN N=RND(1000):GOTO 125
130 CLS:DIM M(4,4,2)
135 FOR MI=0 TO 4:FOR MJ=0 TO 4:FOR KL=1 TO 2
140 M(MI,MJ,KL)=2:NEXT KL,MJ,MI
145 CP=15773+RND(5)+64*(RND(5)-3)
150 CI=RND(5)-3:CJ=RND(5)-3:IF CI=0 AND CJ=0 THEN 150
155 CS=0:GS=0
160 CLS:GOSUB 1000:POKE CP,42
165 GOSUB 2000
170 IF NOT(CX=32 AND CY=32) THEN 185
175 POKE CP,32:CP=NP:POKE CP,42
180 S$=INKEY$:IF S$="" THEN 165 ELSE 135
185 CS=CS+1
190 MI=CI+2:MJ=CJ+2
195 CI=M(MI,MJ,2)
200 IF NOT(CI=2 AND CJ=2) THEN 220
205 CS=CS+1
210 CI=RND(5)-1:CJ=RND(5)-1:IF CI=2 AND CJ=2 THEN 210
215 GOTO 200
220 CI=CI-2:CJ=CJ-2:GOSUB 2000:CI=CI+2:CJ=CJ+2
225 IF NOT(CX=32 AND CY=32) THEN 205
230 M(MI,MJ,1)=CI:M(MI,MJ,2)=CJ
235 CI=CI-2:CJ=CJ-2
240 GS=GS+1:GOSUB 3000:GOSUB 3500
245 GOTO 175
250 REM
255 REM
1000 REM ** BORDER, V.1-TRS-80 **
1005 F0=15360:F1=15423:F2=16128:F3=16191
1010 FOR N=FO TO F1:POKE N,176:NEXT N
1015 FOR N=F2 TO F3:POKE N,131:NEXT N
1020 FOR N=F0 TO F2 STEP 64:POKE N,170:NEXT N
1025 FOR N=F1 TO F3 STEP 64:POKE N,149:NEXT N
1030 POKE F0,160:POKE F1,144:POKE F2,130:POKE F3,129
1035 RETURN
1040 REM
1045 REM
2000 REM ** SEARCH AHEAD, V.1-TRS-80 **
2005 NP=CP:CX=32:CY=32
2010 SI=SGN(CI):SJ=SGN(CJ):AI=ABS(CI):AJ=ABS(CJ)
2015 IF AI=0 THEN 2030 ELSE AI=AI-1
2020 IF SI >0 THEN NP=NP+1 ELSE NP=NP-1
2025 CX=PEEK(NP)
2030 IF AJ-0 THEN 2045 ELSE AJ=AJ-1
2035 IF SJ >0 THEN NP=NP+64 ELSE NP=NP-64
2040 CY=PEEK(NP)
2045 IF NOT(CX=32 AND CY=32) THEN RETURN
2050 IF AI=0 AND AJ=O THEN RETURN ELSE GOTO 2015
2055 REM
2060 REM
3000 REM ** SCORING ROUTINES, V.1-TRS-80 **
3005 AS=GS/CS
3010 PRINT @ 832,"CONTACTS","GOOD SCORE"
3015 PRINT @ 896,STRING$(64,32);
3020 PRINT @ 896,CS,GS;:PRINT @ 928,USING F$;AS;
3025 RETURN
3030 REM
3035 REM
3500 REM ** SHOW BETA MEMORY, V.1-TRS-80 **
3505 W=0
3510 FOR M=0 TO 4:FOR N=0 TO 4:FOR KL=1 TO 2
3515 PRINT @ 966+W,CHR$(M(M,N,KL)+130);
3520 W=W+1
3525 NEXT KL,N,M
3530 RETURN
3535 REM
3540 REM